apply plugin: 'java'
apply plugin: 'maven-publish'

group 'com.vinted'
version '1.0.6'

sourceCompatibility = 1.7

Properties properties = new Properties()
properties.load(project.rootProject.file('deploy.properties').newDataInputStream())

def deployPath = properties.getProperty('aar.deployPath')

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-codec:commons-codec:1.9'
    compile 'com.google.code.gson:gson:2.2.+'

    testCompile 'junit:junit:4.11'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

publishing {
    repositories {
        maven {
            url "file://${deployPath}"
        }
    }
}

test {
  testLogging {
    events 'started', 'passed'
  }
}

task stageFiles(type:Exec) {
    workingDir deployPath
    commandLine 'git','add','.'
}

task commitFiles(type:Exec) {
    workingDir deployPath
    commandLine 'git','commit','-m',"Updated ${project.name} to ${version}"
}

task pushFiles(type:Exec) {
    workingDir deployPath
    commandLine 'git','push','origin','master'
}

publish.finalizedBy stageFiles
stageFiles.finalizedBy commitFiles
commitFiles.finalizedBy pushFiles